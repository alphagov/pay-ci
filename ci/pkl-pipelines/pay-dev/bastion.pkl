amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/shared_resources_for_multi_arch_builds.pkl"
import "../common/PayResources.pkl"

resources {
  shared_resources.payCiGitHubResource
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-dev/bastion.pkl", "master")
  new PayResources.PayGitHubResource {
    name = "bastion-git-release"
    repoName = "pay-aws-bastion"
    source {
      branch = "main"
      tag_regex = "alpha_release-(.*)"
    }
  }
}

jobs {
  new {
    name = "build-and-push-bastion"
    plan {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new GetStep { get = "bastion-git-release" trigger = true }
          new GetStep { get = "pay-ci" }
        }
      }
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new TaskStep {
            task = "parse-release-tag"
            file = "pay-ci/ci/tasks/parse-release-tag.yml"
            input_mapping {
              ["git-release"] = "bastion-git-release"
            }
          }
          new shared_resources_for_multi_arch_builds.AssumeRetagRoleStep {}
          new shared_resources.AssumeRunCodeBuildRoleStep {}
          shared_resources.generateDockerCredsConfigStep
        }
      }

      new InParallelStep {
        in_parallel = new Listing<Step> {
          shared_resources.loadVar("release-number", "tags/release-number")
          shared_resources.loadVar("release-name", "bastion-git-release/.git/ref")
          shared_resources.loadVar("release-sha", "tags/release-sha")
          shared_resources.loadVar("candidate-image-tag", "tags/candidate-tag")
          shared_resources.loadVar("release-image-tag", "tags/tags")
          shared_resources.loadVar("date", "tags/date")
          shared_resources_for_multi_arch_builds.LoadAssumedRetagRoleVar
          shared_resources.LoadRunCodeBuildAssumedRoleVar
        }
      }
      ...shared_resources_for_multi_arch_builds.multiArchCandidateBuild("aws-bastion")
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new shared_resources_for_multi_arch_builds.RetagMultiArchImageInECR { repo = "govukpay/bastion" newTag = "((.:release-image-tag))" }
          new shared_resources_for_multi_arch_builds.RetagMultiArchImageInECR { repo = "govukpay/bastion" newTag = "latest" }
          new shared_resources_for_multi_arch_builds.RetagMultiArchImageInDockerhubAsLatestMaster { repo = "governmentdigitalservice/pay-aws-bastion" }
        }
      }
    }
  }
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-dev/bastion.pkl")
}