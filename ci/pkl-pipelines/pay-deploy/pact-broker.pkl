amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/TaskConfig.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/shared_resources_for_slack_notifications.pkl"
import "../common/shared_resources_for_terraform.pkl"
import "../common/PayResources.pkl"

local typealias SlackNotificationConfig = shared_resources_for_slack_notifications.SlackNotificationConfig

resource_types {
  shared_resources.slackNotificationResourceType
}

resources {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-deploy/pact-broker.pkl", "master")
  shared_resources.payCiGitHubResource
  new PayResources.PayInfraGitHubResource {}
  shared_resources.payECRResourceWithVariant("pact-broker-ecr-registry-deploy",
    "govukpay/pact-broker", "pay_aws_deploy_account_id", "release")
  shared_resources.slackNotificationResource
}

jobs {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-deploy/pact-broker.pkl")
  new {
    name = "deploy-pact-broker"
    plan {
      getStep("pay-ci")
      getStep("pay-infra")
      (getStep("pact-broker-ecr-registry-deploy")) {
        trigger = true
      }
      new TaskStep {
        task = "assume-role"
        file = "pay-ci/ci/tasks/assume-role.yml"
        params {
          ["AWS_ROLE_ARN"] = "arn:aws:iam::((pay_aws_deploy_account_id)):role/pay-concourse-pact-broker-deploy-tooling"
          ["AWS_ROLE_SESSION_NAME"] = "terraform-deploy-assume-role"
        }
      }
      (loadVar("role", "assume-role/assume-role.json")) { format = "json" }
      loadVar("pactbroker_image_tag", "pact-broker-ecr-registry-deploy/tag")
      ...shared_resources_for_terraform.LoadTerraformVersionForTFRootSteps("pay-infra/provisioning/terraform/deployments/deploy/deploy-tooling/environment/pactbroker")
      new TaskStep {
        task = "deploy-pact-broker"
        file = "pay-ci/ci/tasks/deploy-pact-broker.yml"
        params {
          ["ACCOUNT"] = "deploy"
          ["ENVIRONMENT"] = "deploy-tooling"
          ["AWS_ACCESS_KEY_ID"] = "((.:role.AWS_ACCESS_KEY_ID))"
          ["AWS_SECRET_ACCESS_KEY"] = "((.:role.AWS_SECRET_ACCESS_KEY))"
          ["AWS_SESSION_TOKEN"] = "((.:role.AWS_SESSION_TOKEN))"
          ["PACTBROKER_IMAGE_TAG"] = "((.:pactbroker_image_tag))"
        }
      }
      new TaskStep {
        task = "wait-for-deploy"
        file = "pay-ci/ci/tasks/wait-for-deploy.yml"
        params {
          ["ACCOUNT"] = "deploy"
          ["APP_NAME"] = "pactbroker"
          ["ENVIRONMENT"] = "deploy-tooling"
          ["AWS_ACCESS_KEY_ID"] = "((.:role.AWS_ACCESS_KEY_ID))"
          ["AWS_SECRET_ACCESS_KEY"] = "((.:role.AWS_SECRET_ACCESS_KEY))"
          ["AWS_SESSION_TOKEN"] = "((.:role.AWS_SESSION_TOKEN))"
        }
      }
    }
    on_failure = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { message = "Failed to deploy pay-pact-broker image"
        slack_channel_for_failure = "#govuk-pay-owl" }
    )
    on_success = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { is_a_success = true; message = "Deployed pay-pact-broker image" }
    )
  }
}

local function loadVar(variable: String, fileName: String): LoadVarStep = new LoadVarStep {
  load_var = variable
  file = fileName
}

local function getStep(name: String): GetStep = new GetStep {
  get = name
}
