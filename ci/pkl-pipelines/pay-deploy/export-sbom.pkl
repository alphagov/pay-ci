amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/shared_resources_for_deploy_pipelines.pkl"

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-deploy/export-sbom.pkl", "export-sbom")
  (shared_resources.payCiGitHubResource) {
    source {
      branch = "export-sbom"
    }
  }
}

local function getStep(name: String): GetStep = new GetStep {
  get = name
}

jobs = new {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-deploy/export-sbom.pkl")
  new {
    name = "github-export-sbom"
    plan {
      getStep("pay-ci")
      new shared_resources.AssumeConcourseRoleTask {
        aws_account_name = "pay_aws_deploy_account_id"
        session_name = "concourse-assumed-role"
        output_name = "assume-role"
      }
      shared_resources.loadVarJson("role", "assume-role/assume-role.json")
      new TaskStep {
        task = "github-export-sbom"
        file = "pay-ci/ci/tasks/github-export-sbom.yml"
        params {
          ["GITHUB_TOKEN"] = "((github-access-token))"
          ...shared_resources_for_deploy_pipelines.getAWSAssumeRoleCreds()
        }
      }
    }
  }
}

