amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.3#/Pipeline.pkl"

import "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.3#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"

resource_types {
  shared_resources.pullRequestResourceType
}

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-deploy/concourse-runner.pkl", "master")
  shared_resources.payCiGitHubResource
  new shared_resources.PayGitHubPullRequestResource {
    name = "concourse-runner-pr"
    repo = "pay-ci"
    paths {
      "ci/docker/concourse-runner/**"
      "ci/docker/concourse-runner-with-java-17/**";
      "ci/pipelines/concourse-runner.yml"
    }
  }
  new shared_resources.PayGitHubResource {
    name = "adminusers-master"
    repoName = "pay-adminusers"
    source {
      branch = "master"
    }
  }
  new shared_resources.PayGitHubResource {
    name = "webhooks-main"
    repoName = "pay-webhooks"
    source {
      branch = "main"
    }
  }
  new shared_resources.PayGitHubResource {
    name = "concourse-runner-src"
    repoName = "pay-ci"
    source {
      branch = "master"
      paths { "ci/docker/concourse-runner/*" }
    }
  }
  new shared_resources.PayGitHubResource {
    name = "concourse-runner-with-java-17-src"
    repoName = "pay-ci"
    source {
      branch = "master"
      paths { "ci/docker/concourse-runner-with-java-17/*" }
    }
  }
  new shared_resources.PayDockerHubResource {
    name = "concourse-runner"
    source {
      repository = "governmentdigitalservice/pay-concourse-runner"
      tag = "latest"
    }
  }
  new shared_resources.PayDockerHubResource {
    name = "concourse-runner-with-java-17"
    source {
      repository = "governmentdigitalservice/pay-concourse-runner-with-java-17"
      tag = "latest"
    }
  }
}

jobs = new{
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-deploy/concourse-runner.pkl")
  buildAnPushConcourseJob(false, "adminusers-master")
  buildAnPushConcourseJob(true, "webhooks-main")
  new {
    name = "concourse-runner-pr"
    plan {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          getStepWithTrigger("concourse-runner-pr", true)
          getStep("pay-ci")
          getStepWithDepth("adminusers-master", 1)
          getStepWithDepth("webhooks-main", 1)
        }
      }
      putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "pending")
      shared_resources.generateDockerCredsConfigStep
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new TaskStep {
            task = "build-concourse-runner"
            privileged = true
            output_mapping {
              ["image"] = "concourse-runner-image"
            }
            params = new {
              ["CONTEXT"] = "concourse-runner-pr/ci/docker/concourse-runner"
              ["UNPACK_ROOTFS"] = "true"
              ["DOCKER_CONFIG"] = "docker_creds"
            }
            config {
              platform = "linux"
              image_resource = new {
                type = "registry-image"
                source = new {
                  ["repository"] = "concourse/oci-build-task"
                }
              }
              inputs {
                new {
                  name = "concourse-runner-pr"
                }
              }
              outputs {
                new {
                  name = "image"
                }
              }
              run {
                path = "build"
              }
            }
          }
          new TaskStep {
            task = "build-concourse-runner-with-java-17"
            privileged = true
            output_mapping {
              ["image"] = "concourse-runner-with-java-17-image"
            }
            params = new {
              ["CONTEXT"] = "concourse-runner-pr/ci/docker/concourse-runner-with-java-17"
              ["UNPACK_ROOTFS"] = "true"
              ["DOCKER_CONFIG"] = "docker_creds"
            }
            config {
              platform = "linux"
              image_resource = new {
                type = "registry-image"
                source = new {
                  ["repository"] = "concourse/oci-build-task"
                }
              }
              inputs {
                new {
                  name = "concourse-runner-pr"
                }
              }
              outputs {
                new {
                  name = "image"
                }
              }
              run {
                path = "build"
              }
            }
          }
        }
      }
      new InParallelStep {
        in_parallel = new Listing<Step> {
          (testConcourseRunnerTaskX){
            image = "concourse-runner-image"
            input_mapping {
              ["app-repo"] = "adminusers-master"
            }
          }
          (testConcourseRunnerTaskX){
            image = "concourse-runner-with-java-17-image"
            task = "test-concourse-runner-with-java-17"
            input_mapping {
              ["app-repo"] = "webhooks-main"
            }
          }
        }
      }
      putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "success")
    }
    on_failure = putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "failure")
    on_error = putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "failure")
  }

}

local function withPath(path: String) = new Mixin {
  source {
    ["paths"] = new Listing<String> { path }
  }
}

local function withPaths(paths: Listing<String>) = new Mixin {
  source {
    ["paths"] = paths
  }
}

local function getStep(resource: String): GetStep = new {
  get = resource
}

local function getStepWithTrigger(resource: String, _trigger: Boolean): GetStep = new {
  get = resource
  trigger = _trigger
}

local function getStepWithDepth(resource: String, depth: Int): GetStep = new {
  get = resource
  params = new {
    ["depth"] = depth
  }
}

local function putPRTestStatus(resourceName: String, testName: String, status: String): Pipeline.PutStep = new {
  put = resourceName
  params {
    ["path"] = resourceName
    ["status"] = status
    ["context"] = testName
  }
}

local testConcourseRunnerTaskX: Pipeline.TaskStep = new {
  task = "test-concourse-runner"
  privileged = true
  config {
    platform = "linux"
    inputs{
      new {
        name = "pay-ci"
      }
      new {
        name = "app-repo"
      }
    }
    run {
      path = "/bin/bash"
      args {
        "pay-ci/ci/scripts/test-concourse-runner.sh"
      }
    }
  }
  input_mapping = new {
    ["app-repo"] = "sdfsdf"
  }
}

local function testConcourseRunnerTask(imageName: String, appRepo: String): Pipeline.TaskStep = new {
  task = "test-concourse-runner"
  privileged = true
  image = imageName
  config {
    platform = "linux"
    inputs{
      new {
        name = "pay-ci"
      }
      new {
        name = "app-repo"
      }
    }
    run {
      path = "/bin/bash"
      args {
        "pay-ci/ci/scripts/test-concourse-runner.sh"
      }
    }
  }
  input_mapping = new {
    ["app-repo"] = appRepo
  }
}

local function buildAnPushConcourseJob(java17: Boolean, sampleRepo: String): Pipeline.Job = new {
  local versionModifier = if (java17) "-with-java-17" else ""
  local baseName = "concourse-runner\(versionModifier)"

  name = "build-and-push-\(baseName)"
  plan {
    new InParallelStep {
      in_parallel = new Listing<Step> {
        getStepWithTrigger("\(baseName)-src", true)
        getStep("pay-ci")
        getStepWithDepth(sampleRepo, 1)
      }
    }
    shared_resources.generateDockerCredsConfigStep
    new TaskStep {
      task = "build"
      privileged = true
      params = new {
        ["CONTEXT"] = "\(baseName)-src/ci/docker/\(baseName)"
        ["DOCKER_CONFIG"] = "docker_creds"
        ["UNPACK_ROOTFS"] = "true"
      }
      config {
        platform = "linux"
        image_resource = new {
          type = "registry-image"
          source = new {
            ["repository"] = "concourse/oci-build-task"
          }
        }
        inputs {
          new {
            name = "\(baseName)-src"
          }
        }
        outputs {
          new {
            name = "image"
          }
        }
        run {
          path = "build"
        }
      }
    }
    testConcourseRunnerTask("image", sampleRepo)
    new PutStep{
      put = baseName
      params = new {
        ["image"] = "image/image.tar"
      }
    }
  }
}


