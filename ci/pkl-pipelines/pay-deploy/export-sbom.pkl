amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/shared_resources_for_deploy_pipelines.pkl"
import "../common/shared_resources_for_times.pkl"

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-deploy/export-sbom.pkl", "master")
  shared_resources.payCiGitHubResource
  shared_resources_for_times.payWeeklyTimeResource(
    "every-sunday-at-0500",
    "05:00", "05:10",
    "Sunday"
  ) |> shared_resources_for_times.withLocation("UTC")
}

jobs = new {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-deploy/export-sbom.pkl")
  new {
    name = "github-export-sbom"
    plan {
      new InParallelStep {
        in_parallel = new InParallelConfig {
          steps {
            new GetStep { get = "pay-ci" }
            new GetStep {
              get = "every-sunday-at-0500"
              trigger = true
            }
          }
        }
      }
      new shared_resources.AssumeConcourseRoleTask {
        aws_account_name = "deploy"
        role_name = "concourse_sbom_upload_role"
        session_name = "export-sbom"
        output_name = "assume-role"
      }
      shared_resources.loadVarJson("role", "assume-role/assume-role.json")
      new TaskStep {
        task = "github-export-sbom"
        file = "pay-ci/ci/tasks/github-export-sbom.yml"
        params {
          ["GITHUB_TOKEN"] = "((github-access-token))"
          ...shared_resources_for_deploy_pipelines.getAWSAssumeRoleCreds()
        }
      }
    }
  }
}

