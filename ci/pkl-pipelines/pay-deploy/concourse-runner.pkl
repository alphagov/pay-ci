amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/PayResources.pkl"

resource_types {
  shared_resources.pullRequestResourceType
}

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-deploy/concourse-runner.pkl", "master")
  shared_resources.payCiGitHubResource
  new PayResources.PayGitHubPullRequestResource {
    name = "concourse-runner-pr"
    repo = "pay-ci"
    paths {
      "ci/docker/concourse-runner/**"
      "ci/docker/concourse-runner-with-java-17/**"
      "ci/docker/concourse-runner-with-java-21/**"
      "ci/pipelines/concourse-runner.yml"
    }
  }
  new PayResources.PayGitHubResource {
    name = "adminusers-master"
    repoName = "pay-adminusers"
    source {
      branch = "master"
    }
  }
  new PayResources.PayGitHubResource {
    name = "webhooks-main"
    repoName = "pay-webhooks"
    source {
      branch = "main"
    }
  }
  new PayResources.PayGitHubResource {
    name = "concourse-runner-src"
    repoName = "pay-ci"
    source {
      branch = "master"
      paths { "ci/docker/concourse-runner/*" }
    }
  }
  new PayResources.PayGitHubResource {
    name = "concourse-runner-with-java-17-src"
    repoName = "pay-ci"
    source {
      branch = "master"
      paths { "ci/docker/concourse-runner-with-java-17/*" }
    }
  }
  new PayResources.PayGitHubResource {
    name = "concourse-runner-with-java-21-src"
    repoName = "pay-ci"
    source {
      branch = "master"
      paths { "ci/docker/concourse-runner-with-java-21/*" }
    }
  }
  new PayResources.PayDockerHubResource {
    name = "concourse-runner"
    source {
      repository = "governmentdigitalservice/pay-concourse-runner"
      tag = "latest"
    }
  }
  new PayResources.PayDockerHubResource {
    name = "concourse-runner-with-java-17"
    source {
      repository = "governmentdigitalservice/pay-concourse-runner-with-java-17"
      tag = "latest"
    }
  }
  new PayResources.PayDockerHubResource {
    name = "concourse-runner-with-java-21"
    source {
      repository = "governmentdigitalservice/pay-concourse-runner-with-java-21"
      tag = "latest"
    }
  }
}

jobs = new {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-deploy/concourse-runner.pkl")
  buildAndPushConcourseJob("", "adminusers-master")
  buildAndPushConcourseJob("-with-java-17", "webhooks-main")
  buildAndPushConcourseJobWithoutTest("-with-java-21")

  new {
    name = "concourse-runner-pr"
    plan {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          getStepWithTrigger("concourse-runner-pr", true)
          getStep("pay-ci")
          getStepWithDepth("adminusers-master", 1)
          getStepWithDepth("webhooks-main", 1)
        }
      }
      putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "pending")
      shared_resources.generateDockerCredsConfigStep
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new TaskStep {
            task = "build-concourse-runner"
            privileged = true
            output_mapping {
              ["image"] = "concourse-runner-image"
            }
            params = new {
              ["CONTEXT"] = "concourse-runner-pr/ci/docker/concourse-runner"
              ["UNPACK_ROOTFS"] = "true"
              ["DOCKER_CONFIG"] = "docker_creds"
            }
            config {
              platform = "linux"
              image_resource = new {
                type = "registry-image"
                source = new {
                  ["repository"] = "concourse/oci-build-task"
                }
              }
              inputs {
                new {
                  name = "concourse-runner-pr"
                }
              }
              outputs {
                new {
                  name = "image"
                }
              }
              run {
                path = "build"
              }
            }
          }
          new TaskStep {
            task = "build-concourse-runner-with-java-17"
            privileged = true
            output_mapping {
              ["image"] = "concourse-runner-with-java-17-image"
            }
            params = new {
              ["CONTEXT"] = "concourse-runner-pr/ci/docker/concourse-runner-with-java-17"
              ["UNPACK_ROOTFS"] = "true"
              ["DOCKER_CONFIG"] = "docker_creds"
            }
            config {
              platform = "linux"
              image_resource = new {
                type = "registry-image"
                source = new {
                  ["repository"] = "concourse/oci-build-task"
                }
              }
              inputs {
                new {
                  name = "concourse-runner-pr"
                }
              }
              outputs {
                new {
                  name = "image"
                }
              }
              run {
                path = "build"
              }
            }
          }
          new TaskStep {
            task = "build-concourse-runner-with-java-21"
            privileged = true
            output_mapping {
              ["image"] = "concourse-runner-with-java-21-image"
            }
            params = new {
              ["CONTEXT"] = "concourse-runner-pr/ci/docker/concourse-runner-with-java-21"
              ["UNPACK_ROOTFS"] = "true"
              ["DOCKER_CONFIG"] = "docker_creds"
            }
            config {
              platform = "linux"
              image_resource = new {
                type = "registry-image"
                source = new {
                  ["repository"] = "concourse/oci-build-task"
                }
              }
              inputs {
                new {
                  name = "concourse-runner-pr"
                }
              }
              outputs {
                new {
                  name = "image"
                }
              }
              run {
                path = "build"
              }
            }
          }
        }
      }
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new TestConcourseRunnerTask {
            image = "concourse-runner-image"
            task = "test-concourse-runner"
            repo = "adminusers-master"
          }
          new TestConcourseRunnerTask{
            image = "concourse-runner-with-java-17-image"
            task = "test-concourse-runner-with-java-17"
            repo = "webhooks-main"
          }
        }
      }
      putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "success")
    }
    on_failure = putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "failure")
    on_error = putPRTestStatus("concourse-runner-pr", "concourse-runner-tests", "failure")
  }

}

local function withPath(path: String) = new Mixin {
  source {
    ["paths"] = new Listing<String> { path }
  }
}

local function withPaths(paths: Listing<String>) = new Mixin {
  source {
    ["paths"] = paths
  }
}

local function getStep(resource: String): GetStep = new {
  get = resource
}

local function getStepWithTrigger(resource: String, _trigger: Boolean): GetStep = new {
  get = resource
  trigger = _trigger
}

local function getStepWithDepth(resource: String, depth: Int): GetStep = new {
  get = resource
  params = new {
    ["depth"] = depth
  }
}

local function putPRTestStatus(resourceName: String, testName: String, status: String): Pipeline.PutStep = new {
  put = resourceName
  params {
    ["path"] = resourceName
    ["status"] = status
    ["context"] = testName
  }
}

local class TestConcourseRunnerTask extends Pipeline.TaskStep {
  task = "test-concourse-runner"
  privileged = true
  hidden repo: String
  config {
    platform = "linux"
    inputs{
      new {
        name = "pay-ci"
      }
      new {
        name = "app-repo"
      }
    }
    run {
      path = "/bin/bash"
      args {
        "pay-ci/ci/scripts/test-concourse-runner.sh"
      }
    }
  }
  input_mapping = new {
    ["app-repo"] = repo
  }
}

local function buildAndPushConcourseJob(javaVersionSuffix: String, sampleRepo: String): Pipeline.Job = new {
  local baseName = "concourse-runner\(javaVersionSuffix)"

  name = "build-and-push-\(baseName)"
  plan {
    new InParallelStep {
      in_parallel = new Listing<Step> {
        getStepWithTrigger("\(baseName)-src", true)
        getStep("pay-ci")
        getStepWithDepth(sampleRepo, 1)
      }
    }
    shared_resources.generateDockerCredsConfigStep
    new TaskStep {
      task = "build"
      privileged = true
      params = new {
        ["CONTEXT"] = "\(baseName)-src/ci/docker/\(baseName)"
        ["DOCKER_CONFIG"] = "docker_creds"
        ["UNPACK_ROOTFS"] = "true"
      }
      config {
        platform = "linux"
        image_resource = new {
          type = "registry-image"
          source = new {
            ["repository"] = "concourse/oci-build-task"
          }
        }
        inputs {
          new {
            name = "\(baseName)-src"
          }
        }
        outputs {
          new {
            name = "image"
          }
        }
        run {
          path = "build"
        }
      }
    }
    new TestConcourseRunnerTask {
      image = "image"
      repo = sampleRepo
    }
    new PutStep{
      put = baseName
      params = new {
        ["image"] = "image/image.tar"
      }
    }
  }
}

local function buildAndPushConcourseJobWithoutTest(javaVersionSuffix: String): Pipeline.Job = new {
  local baseName = "concourse-runner\(javaVersionSuffix)"

  name = "build-and-push-\(baseName)"
  plan {
    new InParallelStep {
      in_parallel = new Listing<Step> {
        getStepWithTrigger("\(baseName)-src", true)
        getStep("pay-ci")
      }
    }
    shared_resources.generateDockerCredsConfigStep
    new TaskStep {
      task = "build"
      privileged = true
      params = new {
        ["CONTEXT"] = "\(baseName)-src/ci/docker/\(baseName)"
        ["DOCKER_CONFIG"] = "docker_creds"
        ["UNPACK_ROOTFS"] = "true"
      }
      config {
        platform = "linux"
        image_resource = new {
          type = "registry-image"
          source = new {
            ["repository"] = "concourse/oci-build-task"
          }
        }
        inputs {
          new {
            name = "\(baseName)-src"
          }
        }
        outputs {
          new {
            name = "image"
          }
        }
        run {
          path = "build"
        }
      }
    }
    new PutStep{
      put = baseName
      params = new {
        ["image"] = "image/image.tar"
      }
    }
  }
}
