amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/Pipeline.pkl"
import "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.4#/TaskConfig.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"
import "../common/shared_resources_for_multi_arch_builds.pkl"
import "../common/shared_resources_for_test_pipelines.pkl" as shared_test
import "../common/shared_resources_for_slack_notifications.pkl"
import "../common/PayResources.pkl"

local class ImageToCopyFromDockerhubToECR {
  name: String
  repo: String
  tag: String
  image: String = "\(repo):\(tag)"
}

local images_to_copy_from_dockerhub_to_ecr = new Listing<ImageToCopyFromDockerhubToECR> {
  new { name = "postgres-15-alpine" repo = "postgres" tag = "15-alpine" }
  new { name = "localstack-localstack-3" repo = "localstack/localstack" tag = "3" }
  new { name = "selenium-standalone-chrome-3-141-59" repo = "selenium/standalone-chrome" tag = "3.141.59" }
}

local images_to_multi_arch_build = new Listing<shared_resources_for_multi_arch_builds.ImageToMultiArchBuild> {
  new { name = "reverse-proxy" github_repo = "pay-scripts" release_tag_prefix = "reverse_proxy_" }
  new { name = "stubs" github_repo = "pay-stubs" copy_release_to_deploy = true }
  new { name = "zap" github_repo = "pay-scripts" release_tag_prefix = "zap_"}
}

local typealias SlackNotificationConfig = shared_resources_for_slack_notifications.SlackNotificationConfig

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-dev/e2e-helpers.pkl", "master")
  shared_resources.payCiGitHubResource

  new PayResources.PayGitHubResource {
    name = "endtoend-git-release"
    repoName = "pay-endtoend"
    source {
      branch = "master"
      tag_regex = "alpha_release-(.*)"
    }
  }

  new PayResources.PayGitHubResource {
    name = "endtoend-zap-git-release"
    repoName = "pay-endtoend-zap"
    source {
      branch = "main"
      tag_regex = "alpha_release-(.*)"
    }
  }

  for (image_to_build in images_to_multi_arch_build) {
    new shared_resources_for_multi_arch_builds.MultiArchBuildGithubSourceResource { image = image_to_build }
  }

  shared_resources.payECRResource(
    "endtoend-ecr-registry-test", "govukpay/endtoend", "pay_aws_test_account_id"
  ) |> withTag("latest")

  shared_resources.payECRResource(
    "endtoend-zap-ecr-registry-test", "govukpay/endtoend-zap", "pay_aws_test_account_id"
  ) |> withTag("latest")

  shared_resources.payECRResourceWithVariant(
    "endtoend-candidate-ecr-registry-test", "govukpay/endtoend", "pay_aws_test_account_id", "candidate"
  )

  shared_resources.payECRResourceWithVariant(
    "endtoend-zap-candidate-ecr-registry-test", "govukpay/endtoend-zap", "pay_aws_test_account_id", "candidate"
  )

  shared_resources.payDockerHubResource(
    "endtoend-dockerhub", "governmentdigitalservice/pay-endtoend", "latest-master"
  )

  shared_resources.payDockerHubResource(
    "endtoend-zap-dockerhub", "governmentdigitalservice/pay-endtoend-zap", "latest-master"
  )

  for (image_to_build in images_to_multi_arch_build) {
    shared_resources_for_multi_arch_builds.multiArchCandidateECRRepoResource(image_to_build)
  }

  for (image_to_copy in images_to_copy_from_dockerhub_to_ecr) {
    shared_resources.payDockerHubResource(image_to_copy.name, image_to_copy.repo, image_to_copy.tag)
    |> withCheckInterval("1h")

    shared_resources.payECRResource("ecr-\(image_to_copy.name)", image_to_copy.repo, "pay_aws_test_account_id")
    |> withTag(image_to_copy.tag) |> withCheckInterval("never")
  }

  shared_resources.slackNotificationResource
}

groups {
  new {
    name = "copy-from-dockerhub-to-ecr"
    jobs {
      for (image in images_to_copy_from_dockerhub_to_ecr) {
        "copy-\(image.name)"
      }
    }
  }
  new {
    name = "end-to-end"
    jobs {
      "build-and-push-endtoend-candidate"
      "build-and-push-endtoend-zap-candidate"
      "endtoend-e2e"
      "endtoend-zap-e2e"
    }
  }
  for (image in images_to_multi_arch_build) {
    new {
      name = image.name
      jobs {
        "build-and-push-\(image.name)-candidate"
        "\(image.name)-e2e"
      }
    }
  }
  pipeline_self_update.payPipelineSelfUpdateGroup
}

resource_types = new {
  shared_resources.slackNotificationResourceType
}

jobs = new {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-dev/e2e-helpers.pkl")

  new {
    name = "build-and-push-endtoend-candidate"
    plan {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          shared_test.getStep("endtoend-git-release", true, false)
          shared_test.getPayCi
        }
      }

      shared_resources.generateDockerCredsConfigStep
      new shared_resources.ParseGithubAlphaReleaseTagTask { gitRelease = "endtoend-git-release" }
      shared_test.loadVar("release_number_tag", "tags/release-number")
      buildImage("build-endtoend-image", "endtoend-git-release")
      putCandidateImage("endtoend-candidate-ecr-registry-test")
    }

    on_failure = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { message = "Failed to build and push pay-endtoend candidate image"
        slack_channel_for_failure = "#govuk-pay-starling" }
    )
    on_success = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { is_a_success = true; message = "Built and pushed pay-endtoend candidate image" }
    )
  }

  new {
    name = "build-and-push-endtoend-zap-candidate"
    plan {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          shared_test.getStep("endtoend-zap-git-release", true, false)
          shared_test.getPayCi
        }
      }

      shared_resources.generateDockerCredsConfigStep
      new shared_resources.ParseGithubAlphaReleaseTagTask { gitRelease = "endtoend-zap-git-release" }
      shared_test.loadVar("release_number_tag", "tags/release-number")
      buildImage("build-endtoend-zap-image", "endtoend-zap-git-release")
      putCandidateImage("endtoend-zap-candidate-ecr-registry-test")
    }

    on_failure = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { message = "Failed to build and push pay-endtoend-zap candidate image"
        slack_channel_for_failure = "#govuk-pay-starling" }
    )
    on_success = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { is_a_success = true; message = "Built and pushed pay-endtoend-zap candidate image" }
    )
  }

  new {
    name = "endtoend-e2e"
    plan {
      new InParallelStep {
        in_parallel = new Listing {
          new GetStep {
            get = "endtoend-candidate-ecr-registry-test"
            trigger = true
            passed = new Listing { "build-and-push-endtoend-candidate" }
            params { ["format"] = "oci" }
          }
          shared_test.getPayCi
        }
      }

      new shared_resources.ParseECRCandidateTagTask { ecr_repo = "endtoend-candidate-ecr-registry-test" }
      shared_test.loadVar("candidate_number_tag", "parse-candidate-tag/release-number")
      shared_test.assumeCodeBuildRole("executor", "e2e-test-assume-role")

      new InParallelStep {
        in_parallel = new Listing {
          shared_test.loadVar("candidate-image-tag", "endtoend-candidate-ecr-registry-test/tag")
          shared_test.loadAssumeRoleVar
        }
      }

      shared_test.prepareCodeBuild("endtoend", "prepare-e2e-codebuild.yml", "((.:candidate-image-tag))")

      new InParallelStep {
        in_parallel = new Listing {
          shared_test.runCodeBuild("run-codebuild-card", "card.json", 3)
          shared_test.runCodeBuild("run-codebuild-products", "products.json", 1)
        }
      }

      new InParallelStep {
        in_parallel = new Listing {
          putStep(
            "endtoend-ecr-registry-test",
            "endtoend-candidate-ecr-registry-test/image.tar",
            "parse-candidate-tag/release-tag",
            true
          )
          putStep(
            "endtoend-dockerhub",
            "endtoend-candidate-ecr-registry-test/image.tar",
            "",
            true
          )
        }
      }
    }
    on_failure = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { message = "pay-endtoend failed post-merge e2e tests"
        slack_channel_for_failure = "#govuk-pay-starling" }
    )
    on_success = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { is_a_success = true; message = "pay-endtoend passed post-merge e2e tests and was pushed as a final release" }
    )
  }

  new {
    name = "endtoend-zap-e2e"
    plan {
      new InParallelStep {
        in_parallel = new Listing {
          new GetStep {
            get = "endtoend-zap-candidate-ecr-registry-test"
            trigger = true
            passed = new Listing { "build-and-push-endtoend-zap-candidate" }
            params { ["format"] = "oci" }
          }
          shared_test.getPayCi
        }
      }

      new shared_resources.ParseECRCandidateTagTask { ecr_repo = "endtoend-zap-candidate-ecr-registry-test" }
      shared_test.loadVar("candidate_number_tag", "parse-candidate-tag/release-number")
      shared_test.assumeCodeBuildRole("executor", "e2e-test-assume-role")

      new InParallelStep {
        in_parallel = new Listing {
          shared_test.loadVar("candidate-image-tag", "endtoend-zap-candidate-ecr-registry-test/tag")
          shared_test.loadAssumeRoleVar
        }
      }

      shared_test.prepareCodeBuild("endtoend", "prepare-e2e-codebuild.yml", "((.:candidate-image-tag))")

      new InParallelStep {
        in_parallel = new Listing {
          shared_test.runCodeBuild("run-codebuild-zap", "zap.json", 1)
        }
      }

      new InParallelStep {
        in_parallel = new Listing {
          putStep(
            "endtoend-zap-ecr-registry-test",
            "endtoend-zap-candidate-ecr-registry-test/image.tar",
            "parse-candidate-tag/release-tag",
            true
          )
          putStep(
            "endtoend-zap-dockerhub",
            "endtoend-zap-candidate-ecr-registry-test/image.tar",
            "",
            true
          )
        }
      }
    }
    on_failure = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { message = "pay-endtoend-zap failed post-merge e2e tests"
        slack_channel_for_failure = "#govuk-pay-starling" }
    )
    on_success = shared_resources_for_slack_notifications.paySlackNotification(
      new SlackNotificationConfig { is_a_success = true; message = "pay-endtoend-zap passed post-merge e2e tests and was pushed as a final release" }
    )
  }

  for (image_to_build in images_to_multi_arch_build) {
    new shared_resources_for_multi_arch_builds.MultiArchCandidateBuildJob { image = image_to_build }
    new shared_resources_for_multi_arch_builds.MultiArchEndToEndTest { image = image_to_build }
  }

  for (image_to_copy in images_to_copy_from_dockerhub_to_ecr) {
    copyImageToEcr(image_to_copy.name, image_to_copy.image)
  }
}

// THINGS BELOW HERE MAY BE USEFUL IN SHARED_RESOURCES

local function buildImage(taskName: String, context: String): TaskStep = new {
  task = taskName
  privileged = true
  params {
    ["CONTEXT"] = context
    ["DOCKER_CONFIG"] = "docker_creds"
  }
  config {
    platform = "linux"
    image_resource {
      type = "registry-image"
      source { ["repository"] = "concourse/oci-build-task" }
    }
    inputs = new Listing {
      new TaskConfig.Input { name = context }
      new TaskConfig.Input { name = "docker_creds" }
    }
    outputs = new Listing {
      new TaskConfig.Output { name = "image" }
    }
    run { path = "build" }
  }
}

local function putCandidateImage(putName: String): PutStep =
  putStep(putName, "image/image.tar", "tags/candidate-tag", true)


local function copyImageToEcr(image: String, displayName: String): Job = new {
  name = "copy-\(image)"
  plan {
    shared_test.getStep(image, true, true)
    putStep("ecr-\(image)", "\(image)/image.tar", "", true)
  }
  on_failure = shared_resources_for_slack_notifications.paySlackNotification(
    new SlackNotificationConfig { message = "Failed copying \(displayName) image from Docker Hub to ECR"
      slack_channel_for_failure = "#govuk-pay-starling" }
  )
  on_success = shared_resources_for_slack_notifications.paySlackNotification(
    new SlackNotificationConfig { is_a_success = true; message = "Copied \(displayName) image from Docker Hub to ECR" }
  )
}

local function withTag(tag: String) = new Mixin {
  source { ["tag"] = tag }
}

local function withCheckInterval(interval: String) = new Mixin {
  check_every = interval
}

local function putStep(resourceName: String, imageName: String, additionalTags: String, skipDownload: Boolean): PutStep = new PutStep {
  put = resourceName
  params {
    when (imageName.length > 0) {
      ["image"] = imageName
    }
    when (additionalTags.length > 0) {
      ["additional_tags"] = additionalTags
    }
  }
  get_params {
    when (skipDownload) {
      ["skip_download"] = true
    }
  }
}
