amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.1#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"

local concourseTeamName = "pay-dev"

resource_types {
  shared_resources.pullRequestResourceType
}

resources = new {
  // pipeline_self_update.PayPipelineSelfUpdateResource("\(concourseTeamName)/pkl-pipeline-changes.pkl", "main")
  shared_resources.payCiGitHubResource
  (shared_resources.PayGithubPullRequestResource("pkl-pipeline-pr", "pay-ci")) {
    source {
      ["paths"] = new Listing<String> {
        "ci/pkl-pipelines/"
      }
    }
  }
}

groups = new {
  // pipeline_self_update.payPipelineSelfUpdateGroup
  new {
    name = "comment-pipeline-changes-on-pr"
    jobs = new {
      "comment-pipeline-changes-on-pr"
    }
  }
}

jobs = new {
  // pipeline_self_update.PayPipelineSelfUpdateJob("\(concourseTeamName)/pkl-pipeline-changes.pkl")
  new {
    name = "comment-pipeline-changes-on-pr"
    plan = new {
      new InParallelStep {
        in_parallel = new Listing<Step> {
          new GetStep {
            get = "pkl-pipeline-pr"
            trigger = true
            version = "every"
          }
          new GetStep { get = "pay-ci" }
        }
      }
      shared_resources.PutPRTestPendingStatus("pkl-pipeline-pr")
      new TaskStep {
        task = "comment-on-pr-with-yml-and-concourse-diffs"
        file = "pay-ci/ci/tasks/comment-on-pr-with-pkl-pipeline-diffs.yml"
        params = new {
          ["GITHUB_TOKEN"] = "((github-access-token))"
          ["CONCOURSE_TEAM"] = concourseTeamName
          ["FLY_PASSWORD"] = "((readonly_local_user_password))"
        }
        on_success = shared_resources.PutPRTestSuccessStatus("pkl-pipeline-pr")
        on_failure = shared_resources.PutPRTestFailedStatus("pkl-pipeline-pr")
      }
    }
  }
}