amends "package://pkg.pkl-lang.org/github.com/alphagov/pkl-concourse-pipeline/pkl-concourse-pipeline@0.0.1#/Pipeline.pkl"

import "../common/pipeline_self_update.pkl"
import "../common/shared_resources.pkl"

local class PayJsLibraryPipeline {
  name: String
  source_branch: String
}

local payJsLibraries: Listing<PayJsLibraryPipeline> = new {
  new {
    name = "commons"
    source_branch = "master"
  }
  new {
    name = "metrics"
    source_branch = "main"
  }
}

resources = new {
  pipeline_self_update.PayPipelineSelfUpdateResource("pay-js.pkl", "master")
  shared_resources.payCiGitHubResource
  for (jsLibrary in payJsLibraries) {
    new shared_resources.PayGitHubResource {
      name = "js-\(jsLibrary.name)-git-release"
      source = new Mapping {
        ["uri"] = "https://github.com/alphagov/pay-js-\(jsLibrary.name)"
        ["branch"] = jsLibrary.source_branch
        ["commit_filter"] = new {
          exclude = new Listing {
            "\\[automated release\\]"
          }
        }
      }
    }
  }
}

groups = new {
  pipeline_self_update.payPipelineSelfUpdateGroup
  for (jsLibrary in payJsLibraries) {
    new {
      name = "js-\(jsLibrary.name)"
      jobs = new { "version-and-push-\(jsLibrary.name)" }
    }
  }
}

jobs = new {
  pipeline_self_update.PayPipelineSelfUpdateJob("pay-js.pkl")
  for (jsLibrary in payJsLibraries) {
    new {
      name = "version-and-push-\(jsLibrary.name)"
      plan = new {
        new InParallelStep {
          in_parallel = new Listing<Step> {
            new GetStep {
              get = "js-\(jsLibrary.name)-git-release"
              trigger = true
            }
            new GetStep { get = "pay-ci" }
          }
        }
        new TaskStep {
          task = "npm-version-and-create-pr"
          file = "pay-ci/ci/tasks/npm-version-and-create-pr.yml"
          input_mapping = new {
            ["src"] = "js-\(jsLibrary.name)-git-release"
          }
          params = new {
            ["BASE"] = "\(jsLibrary.source_branch)"
            ["REPO"] = "alphagov/pay-js-\(jsLibrary.name)"
            ["GITHUB_TOKEN"] = "((github-access-token))"
          }
        }
      }
    }
  }
}    
